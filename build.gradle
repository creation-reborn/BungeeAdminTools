apply plugin: "java"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "nz.co.lolnet"
archivesBaseName = "BungeeAdminTools"
version = "1.5.3-premium"

configurations {
    provided {
        compile.extendsFrom(provided)
    }
}

repositories {
    jcenter()
    maven {
        name = "md-5"
        url = "https://repo.md-5.net/content/groups/public"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name = "wea-ondara"
        url = "http://repo.wea-ondara.net/repository/public"
    }
}

dependencies {
    provided("com.imaginarycode.minecraft:RedisBungee:0.3.8-INTERIM")
    compile("com.zaxxer:HikariCP:3.2.0")
    provided("mysql:mysql-connector-java:5.1.46")
    provided("net.alpenblock:BungeePerms:3.0-dev-52")
    provided("net.cubespace:Yamler-Core:2.4.0-SNAPSHOT")
    provided("net.md-5:bungeecord-api:1.12-SNAPSHOT")
    compile("org.apache.logging.log4j:log4j-core:2.11.0")
    compile("org.apache.logging.log4j:log4j-slf4j-impl:2.11.0")
    compile("org.slf4j:slf4j-api:1.7.25")
}

jar {
    from {
        (configurations.compile - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)

    from (sourceSets.main.resources.srcDirs) {
        include("plugin.yml")
        expand("name": archivesBaseName, "version": version)
    }
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        jar.outputs.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}